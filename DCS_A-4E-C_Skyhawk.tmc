// DCS A-4E-C Thrustmaster Warthog/AVA/MFD Script by Astro
// TARGET version 3.0.25.127
// version 2.1, Jan 2026


include "target.tmh"
include "DCS_Fighter_Common.tmc"



// -------------------------- INITIALISE ----------------------------

int autopilot;
int ap_path;
int ap_althdg;
int ap_alt;

int extlight_posdim;	


define AB 100											// detent position at AB% of full throttle reach


// -------------------------- MAIN A-4E -----------------------------------------------------------

int main()
{
	// -------------------- COMMON CONFIGURATION --------------------
					
	dcs_fighter_common_configuration();					// load dcs COMMON config
	

	// -------------------- COMMON FUNCTIONS ------------------------
					
	dcs_fighter_common_functions();						// load dcs COMMON functions


	// -------------------- AIRCRAFT FUNCTIONS ----------------------

	// Target Management Switch / Armament Selector (overwrites common)
	TMS_fwd 	= PULSE+'8';																							// Weapon Function Selector Switch - CW
	TMS_aft 	= PULSE+'7';																							// Weapon Function Selector Switch - CCW
	TMS_right 	= 0;																									// 
	TMS_left 	= CHAIN( DOWN+L_SHIFT, D(10), PULSE+'9', D(), UP+L_SHIFT );												// Guns Charging Switch - READY


	// Display Management Switch (overwrites common)
	DMS_fwd 	= SEQ( CHAIN( DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+'d', D(), UP+R_SHIFT+L_SHIFT ),						// DSCG Screen Mode - Radar/TV
					   CHAIN( DOWN+R_SHIFT+L_ALT, D(10), PULSE+'d', D(), UP+R_SHIFT+L_ALT ));							//
	DMS_aft 	= 0;																									// 
	DMS_right 	= CHAIN( R_ALT, D(10), '2' );																			// RWR Search Button
	DMS_left 	= CHAIN( R_ALT, D(10), '5' );																			// RWR Target Separation Button


	// Autopilot function
	autopilot 	= SEQ( CHAIN( EXEC( "autopilot_mode();" ), D(200),														// Set autopilot mode
							  PULSE+'a' ),																				// Autopilot on - AFCS Engage Switch - ENGAGE/OFF
					   PULSE+'a' );																						// Autopilot off - AFCS Engage Switch - ENGAGE/OFF
	
	ap_alt 		= CHAIN( DOWN+R_SHIFT, D(10), PULSE+'a', D(), UP+R_SHIFT, D(200),										// AFCS Altitude Switch - ALT
					   	 DOWN+R_SHIFT+L_CTL, D(10), PULSE+'z', D(), UP+R_SHIFT+L_CTL );									// AFCS Heading Select Switch - OFF
	ap_althdg 	= CHAIN( DOWN+R_SHIFT, D(10), PULSE+'a', D(), UP+R_SHIFT, D(200),										// AFCS Altitude Switch - ALT
					   	 DOWN+R_SHIFT+L_CTL, D(10), PULSE+'a', D(), UP+R_SHIFT+L_CTL );									// AFCS Heading Select Switch - HDG SEL
	ap_path 	= CHAIN( DOWN+R_SHIFT, D(10), PULSE+'z', D(), UP+R_SHIFT, D(200),										// AFCS Altitude Switch - OFF
					   	 DOWN+R_SHIFT+L_CTL, D(10), PULSE+'z', D(), UP+R_SHIFT+L_CTL );									// AFCS Heading Select Switch - OFF


	// Exterior Lighting
	extlight_full 		= CHAIN( DOWN+L_ALT+R_SHIFT, D(10), PULSE+'l', D(), UP+L_ALT+R_SHIFT,	D(100),					// Anti-Collision Light Switch - ON
						   		 DOWN+R_CTL+L_SHIFT, D(10), PULSE+'l', D(), UP+R_CTL+L_SHIFT, D(100),					// Fuselage Light Switch - BRIGHT
						   		 DOWN+L_ALT+L_SHIFT, D(10), PULSE+'l', D(), UP+L_ALT+L_SHIFT, D(100),					// Navigation Lights Switch - BRIGHT
						   		 DOWN+R_ALT+R_SHIFT, D(10), PULSE+'l', D(), UP+R_ALT+R_SHIFT,	D(100),					// Tail Light Switch - BRIGHT
						   		 DOWN+L_SHIFT, D(10), PULSE+'l', D(), UP+L_SHIFT );										// Master Exterior Lights Switch - ON
						   
	// TODO *********
	extlight_posdim 	= CHAIN( DOWN+L_SHIFT+R_CTL, D(10), PULSE+'l', D(), UP+L_SHIFT+R_CTL, D(100),					// Lights Anti Collision OFF
							 	 DOWN+L_CTL+L_ALT, D(10), PULSE+'l', D(), UP+L_CTL+L_ALT, D(100),						// Lights Position Wing DIM
							 	 DOWN+L_ALT+L_SHIFT, D(10), PULSE+'l', D(), UP+L_ALT+L_SHIFT, D(100),					// Lights Position Tail DIM
							 	 PULSE+'l', D(100),																		// Exterior Lights Master Switch ON
							 	 REXEC( 1, 100, "ActKey( PULSE+KEYON+R_CTL+'l' );", RNOSTOP ), 							// Lights Formation Brightnes Down
							 	 D(800),																				//   loop 8x
							  	 EXEC( "StopAutoRepeat(1);" ));															//   stop loop
						   
	extlight_formation 	= CHAIN( DOWN+L_ALT+R_CTL, D(10), PULSE+'l', D(), UP+L_ALT+R_CTL, D(100),						// Anti-Collision Light Switch - OFF
								 DOWN+R_CTL+L_CTL, D(10), PULSE+'l', D(), UP+R_CTL+L_CTL, D(100),						// Fuselage Light Switch - OFF
								 DOWN+L_ALT+L_SHIFT, D(10), PULSE+'l', D(), UP+L_ALT+L_SHIFT, D(100),					// Navigation Lights Switch - BRIGHT
								 DOWN+R_ALT+R_SHIFT, D(10), PULSE+'l', D(), UP+R_ALT+R_SHIFT, D(100),					// Tail Light Switch - BRIGHT
								 DOWN+L_SHIFT, D(10), PULSE+'l', D(), UP+L_SHIFT );										// Master Exterior Lights Switch - ON

	extlight_flashsteady = CHAIN( DOWN+L_SHIFT+L_WIN, D(10), PULSE+'l', D(), UP+L_SHIFT+L_WIN );						// Lighting Flash/Steady Switch - FLSH/STDY

	extlight_covert 	= CHAIN( DOWN+L_CTL, D(10), PULSE+'l', D(), UP+L_CTL );											// Master Exterior Lights Switch - OFF


	// Ground Crew Start-up
	groundcrew = SEQ( CHAIN( PULSE+USB[0x31], D(100), PULSE+F8, D(100), PULSE+F4, D(100), PULSE+F1, D(10000), 			// Radio menu (\), Ground crew, Wheel Chocks, Place 
							 PULSE+USB[0x31], D(100), PULSE+F8, D(100), PULSE+F2, D(100), PULSE+F1, D(13000) ), 		// Radio menu (\), Ground crew, Ground Electric Power, On
					  CHAIN( PULSE+USB[0x31], D(100), PULSE+F8, D(100), PULSE+F2, D(100), PULSE+F2, D(13000), 			// Radio menu (\), Ground crew, Ground Electric Power, Off
							 PULSE+USB[0x31], D(100), PULSE+F8, D(100), PULSE+F4, D(100), PULSE+F2 ));					// Radio menu (\), Ground crew, Wheel Chocks, Remove


	// Speedbrake functions
	speedbrakes_open  = CHAIN( D(100), DOWN+L_SHIFT, D(10), PULSE+'b', D(), UP+L_SHIFT );								// speedbrake extend
	speedbrakes_close = CHAIN( D(100), DOWN+L_CTL, D(10), PULSE+'b', D(), UP+L_CTL );									// speedbrake fully retract


	
	// -------------------- COMMON CONTROLS -------------------------
					
	dcs_fighter_common_controls();						// load dcs COMMON controls
	dcs_fighter_common_mfd();							// load dcs MFD controls
														// before aircraft controls, to allow for overwriting of common controls when necessary


	// -------------------- AIRCRAFT CONTROLS -----------------------

	// ----------------------- LEFT MFD -----------------------------

	MapKeyIO(&LMFD, OSB03, 		CHAIN( DOWN+R_WIN+R_SHIFT, D(10), PULSE+'3', D(), UP+R_WIN+R_SHIFT ), 					// I - Right MFD OSB 03
								TEMPO( CHAIN( DOWN+R_WIN+L_SHIFT, D(10), PULSE+'3', D(), UP+R_WIN+L_SHIFT ),			// O - Left MFD OSB 03
									   CHAIN( DOWN+R_WIN+L_SHIFT, D(10), PULSE+'p', D(), UP+R_WIN+L_SHIFT ), 500 ));	//     [long] Left MFD OSB 03 - Arm Gun



	// ---------------------- FLIGHT STICK --------------------------

	// --DCS_Fighter_Common---------
	// TG - Gun Trigger
	// S1 - Nosewheel Steering Button
	// H1 - Trim Switch
	// H2 - Target Management Switch
	// H3 - Display Management Switch
	// JOYX
	// JOYY
	// -----------------------------	



	// Weapon Release Button
	// S2
	MapKeyIO(&AVA_F16, S2,  	SEQ( CHAIN( L_ALT+L_SHIFT, D(10), '6' ),												// I - Emergency Release Selector - AL
								 	 CHAIN( L_CTL, D(10), 'j' )),														//	   Emergency Stores Release Handle
								CHAIN( L_ALT, D(10), SPC ));															// O - weapon release


	// Expand FOV Button - S3
	MapKey(&AVA_F16, S3,  		0 );																					// 
	MapKeyR(&AVA_F16, S3,  		0 );																					// 

/*
	// Display Management Switch  ---------- To Common
	// H3U
	MapKeyIO(&AVA_F16, H3U, 	TEMPO( CHAIN( DOWN+R_SHIFT, D(10), PULSE+'k', D(), UP+R_SHIFT ),						// I - kneeboard on/off
									   nightvision, 500 ),																// 	   [long] Night Vision Goggles toggle
								SEQ( CHAIN( DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+'d', D(), UP+R_SHIFT+L_SHIFT ),			// O - DSCG Screen Mode - Radar/TV
									 CHAIN( DOWN+R_SHIFT+L_ALT, D(10), PULSE+'d', D(), UP+R_SHIFT+L_ALT )));			//
	// H3D
	MapKeyIO(&AVA_F16, H3D, 	TEMPO( CHAIN( DOWN+R_CTL, D(10), PULSE+'k', D(), UP+R_CTL ),							// I - kneeboard current position mark point
									   flashlight, 500 ),																//	   [long] Flashlight on/off	
								CHAIN( 0 ));																			// O - 
	// H3R
	MapKeyIO(&AVA_F16, H3R, 	TEMPO( PULSE+']', 																		// I - kneeboard next page
									   CHAIN( DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+']', D(), UP+R_SHIFT+L_SHIFT ), 500 ),	//	   [long] Kneeboard Next Shortcut
								CHAIN( R_ALT, D(10), '2' ));															// O - RWR Search Button
	// H3L
	MapKeyIO(&AVA_F16, H3L, 	TEMPO( PULSE+'[', 																		// I - kneeboard previous page
									   CHAIN( DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+'[', D(), UP+R_SHIFT+L_SHIFT ), 500 ),	//	   [long] Kneeboard Previous Shortcut
								CHAIN( R_ALT, D(10), '5' ));															// O - RWR Target Separation Button
*/

 	// Countermeasure Management Switch
	// H4U	
	MapKeyIO(&AVA_F16, H4U, 	0,																						// I - 
							 	CHAIN( DOWN+L_ALT+L_SHIFT, D(10), PULSE+'q', D(), UP+L_ALT+L_SHIFT ));					// O - Chaff Dispenser Select - 1
	// H4D
	MapKeyIO(&AVA_F16, H4D, 	0, 																						// I - 
							 	CHAIN( DOWN+L_ALT+L_CTL, D(10), PULSE+'q', D(), UP+L_ALT+L_CTL ));						// O - Chaff Dispenser Select - 2
	// H4R
	MapKeyIO(&AVA_F16, H4R, 	CHAIN( DOWN+L_ALT+L_SHIFT, D(10), PULSE+'j', D(), UP+L_ALT+L_SHIFT ),					// I - AN/APR-25 Function Selector Switch - CW
			  				 	CHAIN( DOWN+L_ALT+R_SHIFT, D(10), PULSE+'q', D(), UP+L_ALT+R_SHIFT ));					// O - Chaff Dispenser Select - Both
	// H4L
	MapKeyIO(&AVA_F16, H4L, 	CHAIN( DOWN+L_ALT+L_CTL, D(10), PULSE+'j', D(), UP+L_ALT+L_CTL ),						// I - AN/APR-25 Function Selector Switch - CCW
							 	CHAIN( DOWN+L_ALT+R_CTL, D(10), PULSE+'q', D(), UP+L_ALT+R_CTL )); 						// O - Chaff Power Switch - PWR
	// H4P
	MapKeyIO(&AVA_F16, H4P, 	0, 																						// I - 
							 	PULSE+'q' ); 																			// O - JATO Firing Button (ALE-29A Burst)
	
	
 
	// ------------------------ THROTTLE ----------------------------

	// --DCS_Fighter_Common---------
	// Left Throttle Button
	// SPDF/B
	// CHF/B
	// MSU/D/P
	// PSF/B
	// EACON/OFF Landing Gear
	// EOLIGN/OVER
	// EORIGN/OVER
	// EFLNORM/OVER
	// EFRNORM/OVER
	// SC
	// APUON/OFF
	// RDRNRM/DIS Canopy
	// IDLELON/IDLERON
	// THR_LEFT
	// THR_RIGHT
	// THR_FC
	// SCX
	// SCY
	// -----------------------------



	// Autopilot
	// APENG	
	MapKey(&Throttle, APENG, 	autopilot );																	// Autopilot on/off
	// APDIS
	MapKey(&Throttle, APDIS, 	0 ); 																			// see autopilot_mode()
	// APPAT
	MapKey(&Throttle, APPAT, 	ap_path );																		// see autopilot_mode()
	// APAH
	MapKey(&Throttle, APAH, 	ap_althdg );																	// see autopilot_mode()
	// APALT
	MapKey(&Throttle, APALT, 	ap_alt );																		// see autopilot_mode()


	// Coolie Switch
	// CSU
	MapKeyIOUMD(&Throttle, CSU, CHAIN( L_SHIFT+L_CTL, D(10), 'v' ), 											// IU - Missile Volume Knob - CCW/Decrease
								0, 																				// OU - 
								EXEC( "if(CAR_mode) ActKey( PULSE+KEYON+L_CTL+'q' );"							// IM - [CAR] Nose strut extend
									  "else ActKey( PULSE+KEYON+L_SHIFT+'`' );" ),								//      Previous Waypoint
								0, 																				// OM - 
								CHAIN( L_SHIFT+L_CTL, D(10), 'v' ), 											// IU - Missile Volume Knob - CCW/Decrease
								0 ); 																			// OD - 
	// CSD
	MapKeyIOUMD(&Throttle, CSD, CHAIN( L_SHIFT+L_CTL, D(10), 'f' ), 											// IU - Missile Volume Knob - CW/Increase
								0,	 																			// OU - 
								EXEC( "if(CAR_mode) ActKey( PULSE+KEYON+L_CTL+'a' );"							// IM - [CAR] Nose strut kneel
									  "else ActKey( PULSE+KEYON+L_CTL+'`' );" ),								//      Next Waypoint
								0,	 																			// OM - 
								CHAIN( L_SHIFT+L_CTL, D(10), 'f' ), 											// IU - Missile Volume Knob - CW/Increase
								0 );	 																		// OD - 
	// CSR
	MapKeyIOUMD(&Throttle, CSR, 0,																				// IU - 
								0,																				// OU - 
								CHAIN( R_SHIFT+L_WIN, D(10), 'a' ),												// IM - AFCS Heading Select Pull-to-Set Knob - Continuous Increase
								TEMPO( 0, PULSE+'m', 500 ),														// OM - [long] Toggle Mirrors
								0,																				// ID - 
								0 );																			// OD - 
	// CSL
	MapKeyIOUMD(&Throttle, CSL, 0,																				// IU - 
								0,																				// OU - 
								CHAIN( R_SHIFT+L_WIN, D(10), 'z' ),												// IM - AFCS Heading Select Pull-to-Set Knob - Continuous Decrease
								0,																				// OM - 
								0,																				// ID - 
								0 );																			// OD - 

/*								
	// China Hat  // Todo move to Common
	// CHF
	MapKeyIO(&Throttle, CHF, 	brightness_up, 																	// I - brightness up for reticle/NVG
							 	'-' ); 			// change to DX13																// O - Radar Antenna Tilt - Continuous CW
	// CHB
	MapKeyIO(&Throttle, CHB, 	brightness_down,																// I - brightness down for reticle/NVG
							 	'=' ); 			// change to DX14																// O - Radar Antenna Tilt - Continuous CCW
*/


	// Pinky Switch (exterior lighting)
	// PSF
	MapKeyIO(&Throttle, PSF, 	CHAIN( extlight_flashsteady, D(), 												// I - extlight_flashsteady toggle
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) ), 							//     LED4 ON
							 	CHAIN( extlight_full, D(), 														// O - extlight_full
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) )); 							//     LED4 ON
	MapKeyRIO(&Throttle, PSF, 	CHAIN( 0, D(), 																	// I - 
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED4) ),							//     LED4 OFF
							  	CHAIN( extlight_formation, D(), 												// O - extlight_formation
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED4) )); 							//     LED4 OFF
	// PSB		
	MapKeyIO(&Throttle, PSB, 	CHAIN( 0, D(), 																	// I - 
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) ), 							//     LED4 ON
							 	CHAIN( extlight_covert, D(), 													// O - extlight_covert
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) )); 							//     LED4 ON
	MapKeyRIO(&Throttle, PSB, 	CHAIN( 0, D(), 																	// I - 
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED4) ),							//     LED4 OFF
							  	CHAIN( extlight_formation, D(), 												// O - extlight_formation
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED4) )); 							//     LED4 OFF


	// Flaps Switch
	// FLAPU
	MapKey(&Throttle, FLAPU,  	CHAIN( DOWN+L_SHIFT, D(10), PULSE+'f', D(), UP+L_SHIFT ));						// Flap Handle - UP
	// FLAPM
	MapKey(&Throttle, FLAPM, 	CHAIN( DOWN+L_ALT, D(10), PULSE+'f', D(), UP+L_ALT ));							// Flap Handle - STOP
	// FLAPD	
	MapKey(&Throttle, FLAPD,  	CHAIN( DOWN+L_CTL, D(10), PULSE+'f', D(), UP+L_CTL ));							// Flap Handle - DOWN


	// Engine Operation
	// EOLIGN
	MapKeyIO(&Throttle, EOLIGN,	CHAIN( DOWN+R_SHIFT, D(10), PULSE+HOME, D(), UP+R_SHIFT ), 						// I - Engine Starter Switch - START
								CHAIN( DOWN+L_SHIFT, D(10), PULSE+HOME, D(), UP+L_SHIFT ));						// O - Throttle Position Lock - Step Up (OFF/IGN/IDLE)
	// EORIGN
	MapKey(&Throttle, EORIGN, 	0 ); 																			// 


	// U LAYER - [O] Air-to-Air Mode
	//			 [I] ACM Mode
	// BSF - LED1 ON/OFF
	MapKeyIO(&Throttle, BSF, 	CHAIN( 0, D(200),																		// I - 
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED1) ), 									// 	   LED1 ON for U layer
								CHAIN( 0, D(200),																		// O - A-A mode on
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED1) )); 									// 	   LED1 ON for U layer
	MapKeyRIO(&Throttle, BSF, 	CHAIN( 0, D(200),																		// I - 
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED1) ), 									// 	   LED1 OFF for U layer 
								CHAIN( 0, D(200),																		// O - A-A mode off
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED1) )); 									// 	   LED1 OFF for U layer

	// M LAYER - Navigation Mode
	// BSM - LED2 ON/OFF
	MapKey(&Throttle, BSM, 		LED(&Throttle, LED_ONOFF, LED_CURRENT+LED2) ); 											// LED2 ON for M layer
	MapKeyR(&Throttle, BSM, 	LED(&Throttle, LED_ONOFF, LED_CURRENT-LED2) ); 											// LED2 OFF for M layer

	// D LAYER - Air-to-Ground Mode
	// BSB - LED3 ON/OFF
	MapKeyIO(&Throttle, BSB, 	CHAIN( EXEC( "Throttle[BSB] = 0; DefEventHandler(EV_HID_INPUT_DATA, &Throttle, BSB);"),	// I - Temporary virtual M layer (reset D layer)
									   EXEC( "CAR_mode = 1;"															// 	   Set CAR_mode ON
											 "printf(\"CAR_mode = 1\\xa\");" ),											// 
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED5) ),									//     LED5 ON
								CHAIN( 0, D(100),																		// O - A-G mode on
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED3) )); 									//     LED3 ON for D layer
	MapKeyRIO(&Throttle, BSB, 	CHAIN( EXEC( "CAR_mode = 0;"															// I - Set CAR_mode OFF
											 "printf(\"CAR_mode = 0\\xa\");" ),											// 
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED5) ),									//     LED5 OFF
								CHAIN( 0, D(),																			// O - A-G mode off
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED3) ));									// 	   LED3 OFF for D layer



}	// =====  end main()  =========================================================================


// ---------------------- AUTOPILOT MODE ----------------------------

int autopilot_mode()
{
	if (Throttle[APPAT]) ActKey(KEYON+PULSE+ap_path);									// Autopilot - Path
	else if (Throttle[APALT]) ActKey(KEYON+PULSE+ap_alt);								// Autopilot - Alt
	else ActKey(KEYON+PULSE+ap_althdg);													// Autopilot - Alt/Hdg
}


// ------------------------ EVENTHANDLE -----------------------------

int EventHandle(int type, alias o, int x)
{
	DefaultMapping(&o, x);
}
