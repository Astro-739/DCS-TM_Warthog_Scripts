// DCS F-16C Viper Thrustmaster Warthog/AVA/MFD Script by Astro
// TARGET version 3.0.24.618
// version 2.1, Nov 2025


include "target.tmh"
include "DCS_Fighter_Common.tmc"


// -------------------------- INITIALISE ----------------------------

int autopilot_ALThold;	
int autopilot_ATThold;
int autopilot_off;
int autopilot;
char autopilot_mode;

int override;	
char override_dog;		

define AB 75											// detent position at AB% of full throttle reach


// -------------------------- MAIN F-16C ----------------------------------------------------------

int main()
{
	// -------------------- COMMON CONFIGURATION --------------------
					
	dcs_fighter_common_configuration();					// load dcs COMMON config
	

	// -------------------- COMMON FUNCTIONS ------------------------
					
	dcs_fighter_common_functions();						// load dcs COMMON functions


	// -------------------- AIRCRAFT FUNCTIONS ----------------------
	
	// Start-up Sequence (overwrites common)
	startup = SEQ( CHAIN( L_ALT, D(10), 's' ), 												// MAIN PWR Switch - BATT
				   CHAIN( R_SHIFT, D(10), 's' ), 											// MAIN PWR Switch - MAIN PWR
				   CHAIN( R_CTL, D(10), 's' )); 											// JFS Switch - START 1

	// Autopilot function
	autopilot_ALThold = CHAIN( DOWN+L_CTL+L_SHIFT, D(10), DOWN+'p', D(100), UP+'p', D(), UP+L_CTL+L_SHIFT,		// Autopilot PITCH Switch - ALT HOLD  (extra 100ms needed by DCS)
							   EXEC( "autopilot_mode = 1;" ));
	
	autopilot_ATThold = CHAIN( DOWN+L_CTL+L_ALT, D(10), DOWN+'p', D(100), UP+'p', D(), UP+L_CTL+L_ALT,			// Autopilot PITCH Switch - ATT HOLD  (extra 100ms needed by DCS)
							   EXEC( "autopilot_mode = 1;" ));
	
	autopilot_off = CHAIN( DOWN+L_CTL, D(10), DOWN+'p', D(100), UP+'p', D(), UP+L_CTL, 							// Autopilot PITCH Switch - A/P OFF   (extra 100ms needed by DCS)
						   EXEC( "autopilot_mode = 0;" ));
	
	autopilot_mode = 0;

	autopilot = EXEC( "if(!autopilot_mode) ActKey( PULSE+KEYON+autopilot_ALThold );"
					  "if(autopilot_mode) ActKey( PULSE+KEYON+autopilot_off );"	);


	// External Lights
	extlight_full		= CHAIN( DOWN+R_SHIFT, D(10), PULSE+'l', D(), UP+R_SHIFT, D(100), 						// MASTER Switch - NORM
								 DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+'l', D(), UP+R_SHIFT+L_SHIFT, D(100),		// WING/TAIL Switch - BRT
								 DOWN+R_CTL+L_SHIFT, D(10), PULSE+'l', D(), UP+R_CTL+L_SHIFT, D(),				// FUSELAGE Switch - BRT
								 LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) );									// LED4 ON
	extlight_position	= CHAIN( DOWN+R_CTL, D(10), PULSE+'l', D(), UP+R_CTL, D(100),							// MASTER Switch - A-C covert
								 DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+'l', D(), UP+R_SHIFT+L_SHIFT, D(100),		// WING/TAIL Switch - BRT
								 DOWN+R_CTL+L_CTL, D(10), PULSE+'l', D(), UP+R_CTL+L_CTL, D(),					// FUSELAGE Switch - OFF
								 LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) );									// LED4 ON
	extlight_formation  = 0;
	extlight_covert		= CHAIN( DOWN+L_CTL, D(10), PULSE+'l', D(), UP+L_CTL, D(100), 							// MASTER Switch - ALL covert
								 DOWN+R_SHIFT+L_CTL, D(10), PULSE+'l', D(), UP+R_SHIFT+L_CTL, D(100),			// WING/TAIL Switch - OFF
								 DOWN+R_CTL+L_CTL, D(10), PULSE+'l', D(), UP+R_CTL+L_CTL, D(),					// FUSELAGE Switch - OFF
								 LED(&Throttle, LED_ONOFF, LED_CURRENT-LED4) );									// LED4 OFF
	extlight_flashsteady = CHAIN( DOWN+R_ALT, D(10), PULSE+'l', D(), UP+R_ALT );								// FLASH STEADY Switch - FLASH/STEADY

//	extlight_dimbright 		SEQ( CHAIN( DOWN+R_SHIFT+L_ALT, D(10), PULSE+'l', D(), UP+R_SHIFT+L_ALT, D(),		// WING/TAIL Switch - DIM
//										DOWN+R_CTL+L_ALT, D(10), PULSE+'l', D(), UP+R_CTL+L_ALT ),				// FUSELAGE Switch - DIM
//								 CHAIN( DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+'l', D(), UP+R_SHIFT+L_SHIFT, D(),	// WING/TAIL Switch - BRT
//										DOWN+R_CTL+L_SHIFT, D(10), PULSE+'l', D(), UP+R_CTL+L_SHIFT )),			// FUSELAGE Switch - BRT


	// A-A override function
	override_dog = 0;
	
	override = EXEC( "if(!override_dog) ActKey( PULSE+KEYON+L_CTL+'3' );"										// DOGFIGHT/Missile Override Switch - DOGFIGHT
					 "if(override_dog) ActKey( PULSE+KEYON+L_CTL+'4' );"										// DOGFIGHT/Missile Override Switch - MISSILE OVERRIDE
					 "override_dog = !override_dog;" 															// switch override_dog
					 "printf(\"override_dogfight = %i\\xa\", override_dog);" );									// print state to output window





	// -------------------- COMMON CONTROLS -------------------------
					
	dcs_fighter_common_controls();						// load dcs COMMON controls
	dcs_fighter_common_mfd();							// load dcs MFD controls
														// before aircraft controls, to allow for overwriting of common controls when necessary


	// -------------------- AIRCRAFT CONTROLS -----------------------

	// ---------------------- FLIGHT STICK --------------------------

	// --DCS_Fighter_Common---------
	// TG - Camera/Gun Trigger
	// S1 - Nosewheel Steering Button
	// S2 - Weapon Release Button
	// H1 - Trim Switch
	// H3 - Display Management Switch
	// JOYX
	// JOYY
	// -----------------------------	


	// Nosewheel Steering Button
	// S1
	MapKeyIOUMD(&AVA_F16, S1, 	SEQ( PULSE+F10, PULSE+F1 ),														// IU - F10 map
							 	's', 																			// OU - NWS A/R DISC MSL STEP Button
							    SEQ( PULSE+F10, PULSE+F1 ), 													// IM - F10 map
							    's',																 			// OM - NWS A/R DISC MSL STEP Button
							    SEQ( PULSE+F10, PULSE+F1 ),								 						// ID - F10 map
							    TEMPO( PULSE+'s',																// OD - [short] NWS A/R DISC MSL STEP Button
									   CHAIN( DOWN+L_ALT+R_SHIFT, D(10), PULSE+'6', D(), UP+L_ALT+R_SHIFT ),	//		[long]  Switch AG weapon type (SMS page Right MFD OSB 6)
									   500 ));


	// Expand FOV Button - S3
	MapKeyUMD(&AVA_F16, S3,     override,																		// U - dogfight then missile override
							    autopilot,															 			// M - autopilot on/off (Autopilot PITCH Switch - ALT HOLD)
							    0);																				// D - 


	// Target Management Switch
	// H2U
	MapKeyIO(&AVA_F16, H2U, 	CHAIN( R_CTL, D(10), UARROW ),													// I - TMS up
							 	EXEC( "if (VR_mode) ActKey ( KEYON+DX15 );"										// O - VR zoom
									  "else ActKey ( KEYON+DX7 );" ));											//     TrackIR zoom in
	MapKeyRIO(&AVA_F16, H2U, 	0,																				// I - 
							 	EXEC( "if (VR_mode) ActKey ( DX15 );"											// O - Release DX15
									  "else ActKey ( DX7 );" ));												// 	   Release DX7
	// H2D
	MapKeyIO(&AVA_F16, H2D, 	CHAIN( R_CTL, D(10), DARROW ),													// I - TMS down
							 	EXEC( "if (VR_mode) ActKey ( PULSE+KEYON+L_ALT+'c' );"							// O - VR toggle mouse pointer
									  "else ActKey ( KEYON+DX9 );" ));											// 	   TrackIR zoom out
	MapKeyRIO(&AVA_F16, H2D, 	0,																				// I - 
							 	EXEC( "if (VR_mode) ActKey ( 0 );"												// O - 
									  "else ActKey ( DX9 );" ));												// 	   Release DX9
	// H2R							
	MapKeyIO(&AVA_F16, H2R, 	CHAIN( R_CTL, D(10), RARROW ),													// I - TMS right
							 	EXEC( "if (VR_mode) ActKey ( KEYON+DX18 );"										// O - VR Right mouse click
									  "else ActKey ( KEYON+DX8 );" ));											// 	   TrackIR snap view right MFD (LWIN+KP6)
	MapKeyRIO(&AVA_F16, H2R, 	0,																				// I - 
							 	EXEC( "if (VR_mode) ActKey ( DX18 );"											// O - Release DX18
									  "else ActKey ( DX8 );" ));												// 	   Release DX8
	// H2L
	MapKeyIO(&AVA_F16, H2L, 	CHAIN( R_CTL, D(10), LARROW ),													// I - TMS left
							 	EXEC( "if (VR_mode) ActKey ( KEYON+DX17 );"										// O - VR Left mouse click
									  "else ActKey ( KEYON+DX10 );" ));											// 	   TrackIR snap view left MFD (LWIN+KP4)
	MapKeyRIO(&AVA_F16, H2L, 	0,																				// I - 
							 	EXEC( "if (VR_mode) ActKey ( DX17 );"											// O - Release DX17
									  "else ActKey ( DX10 );" ));												// 	   Release DX10


 	// Countermeasure Management Switch
	// H4U
	MapKeyIO(&AVA_F16, H4U, 	CHAIN( DOWN+L_SHIFT+L_CTL, D(10), PULSE+'f', D(), UP+L_SHIFT+L_CTL ),			// I - ECM XMIT Switch - Up
							 	CHAIN( DOWN+R_SHIFT, D(10), PULSE+';', D(), UP+R_SHIFT ));						// O - Countermeasures Management Switch - Fwd
	// H4D
	MapKeyIO(&AVA_F16, H4D, 	CHAIN( DOWN+L_SHIFT+L_CTL, D(10), PULSE+'v', D(), UP+L_SHIFT+L_CTL ),			// I - ECM XMIT Switch - Down
							 	CHAIN( DOWN+R_SHIFT, D(10), PULSE+'.', D(), UP+R_SHIFT )); 						// O - Countermeasures Management Switch - Aft
	// H4R
	MapKeyIO(&AVA_F16, H4R, 	CHAIN( DOWN+L_CTL, D(10), PULSE+'f', D(), UP+L_CTL ), 							// I - PROGRAM Knob - CW
			  				 	CHAIN( DOWN+R_SHIFT, D(10), PULSE+'/', D(), UP+R_SHIFT ));						// O - Countermeasures Management Switch - Right
	// H4L
	MapKeyIO(&AVA_F16, H4L, 	CHAIN( DOWN+L_SHIFT, D(10), PULSE+'f', D(), UP+L_SHIFT ), 						// I - PROGRAM Knob - CCW
							 	CHAIN( DOWN+R_SHIFT, D(10), PULSE+',', D(), UP+R_SHIFT )); 						// O - Countermeasures Management Switch - Left
	// H4P
	MapKeyIO(&AVA_F16, H4P, 	0, 																				// I - 
							 	CHAIN( DOWN+L_ALT, D(10), DOWN+'f', D(100), UP+'f', D(), UP+L_ALT ));			// O - CHAFF/FLARE Dispense Button (program 5)



 
	// ------------------------ THROTTLE ----------------------------

	// --DCS_Fighter_Common---------
	// Left Throttle Button
	// SPDF/B
	// CHF/B
	// MSU/D/P
	// PSF/B
	// EACON/OFF Landing Gear
	// EOLIGN/OVER
	// EORIGN/OVER
	// EFLNORM/OVER
	// EFRNORM/OVER
	// SC
	// APUON/OFF
	// RDRNRM/DIS Canopy
	// IDLELON/IDLERON
	// THR_LEFT
	// THR_RIGHT
	// THR_FC
	// SCX
	// SCY
	// -----------------------------



	// Autopilot
	// APENG	
	MapKeyIO(&Throttle, APENG, 	autopilot_ATThold,																// I - Autopilot ATT on (Autopilot PITCH Switch - ATT HOLD)
								autopilot );																	// O - Autopilot ALT on or AP Off (Autopilot PITCH Switch - ALT HOLD)
	// APDIS
	MapKey(&Throttle, APDIS, 	0); 																			// 
	// APPAT
	MapKey(&Throttle, APPAT, 	CHAIN( DOWN+L_SHIFT, D(10), PULSE+'a', D(), UP+L_SHIFT )); 						// Autopilot ROLL Switch - HDG SEL
	// APAH
	MapKey(&Throttle, APAH, 	CHAIN( DOWN+L_CTL, D(10), PULSE+'a', D(), UP+L_CTL ));							// Autopilot ROLL Switch - ATT HOLD
	// APALT
	MapKey(&Throttle, APALT, 	CHAIN( DOWN+L_ALT, D(10), PULSE+'a', D(), UP+L_ALT ));							// Autopilot ROLL Switch - STRG SEL


	// Coolie Switch
	// CSU
	MapKeyIOUMD(&Throttle, CSU, CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x56], D(), UP+L_CTL ),						// IU - ICP DED Increment/Decrement Switch - Decrement   (L_CTL+NUM-)
								CHAIN( DOWN+L_ALT+R_SHIFT, D(10), PULSE+'9', D(), UP+L_ALT+R_SHIFT ),			// OU - Slave/Bore toggle (Right MFD OSB 19 on SMS page)
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x56], D(), UP+L_CTL ),						// IM - ICP DED Increment/Decrement Switch - Decrement   (L_CTL+NUM-)
								0, 																				// OM - 
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x56], D(), UP+L_CTL ),						// ID - ICP DED Increment/Decrement Switch - Decrement   (L_CTL+NUM-)
								0);																				// OD - 
	// CSD
	MapKeyIOUMD(&Throttle, CSD, CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x57], D(), UP+L_CTL ),						// IU - ICP DED Increment/Decrement Switch - Increment   (L_CTL+NUM+)
								DX24,																			// OU - ENABLE Switch - Depress
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x57], D(), UP+L_CTL ),						// IM - ICP DED Increment/Decrement Switch - Increment   (L_CTL+NUM+)
								DX24, 																			// OM - ENABLE Switch - Depress
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x57], D(), UP+L_CTL ),						// ID - ICP DED Increment/Decrement Switch - Increment   (L_CTL+NUM+)
								DX24);																			// OD - ENABLE Switch - Depress
	// CSR
	MapKeyIOUMD(&Throttle, CSR, CHAIN( L_CTL, D(10), RARROW ), 													// IU - ICP Data Control Switch - SEQ (dobber sequence)
							 	DX23,																			// OU - UNCAGE Switch
								CHAIN( L_CTL, D(10), RARROW ), 													// IM - ICP Data Control Switch - SEQ (dobber sequence)
								TEMPO( CHAIN( DOWN+R_CTL+R_SHIFT, D(10), PULSE+'v', D(), UP+R_CTL+R_SHIFT ),	// OM - FUEL QTY SEL Knob - CW
									   CHAIN( DOWN+R_CTL+R_SHIFT, D(10), PULSE+'c', D(), UP+R_CTL+R_SHIFT ), 	//		[long] FUEL QTY SEL Knob - NORM
									   500 ),																	//
								CHAIN( L_CTL, D(10), RARROW ), 													// ID - ICP Data Control Switch - SEQ (dobber sequence)
								DX23 );																			// OD - UNCAGE Switch
	// CSL
	MapKeyIOUMD(&Throttle, CSL, CHAIN( L_CTL, D(10), LARROW ), 													// IU - ICP Data Control Switch - RET (dobber return)
								TEMPO( CHAIN( L_CTL, D(10), KP7 ),												// OU - ICP Priority Function Button - 7 (MARK)
									   CHAIN( L_CTL, D(10), KP0 ), 500 ),										//		[long] ICP Priority Function Button - 0 (M-SEL)
								CHAIN( L_CTL, D(10), LARROW ), 													// IM - ICP Data Control Switch - RET (dobber return)
								TEMPO( CHAIN( L_CTL, D(10), KP7 ),												// OM - ICP Priority Function Button - 7 (MARK)
									   CHAIN( L_CTL, D(10), KP0 ), 500 ),										//		[long] ICP Priority Function Button - 0 (M-SEL)
								CHAIN( L_CTL, D(10), LARROW ), 													// ID - ICP Data Control Switch - RET (dobber return)
								TEMPO( CHAIN( L_CTL, D(10), KP7 ),												// OD - ICP Priority Function Button - 7 (MARK)
									   CHAIN( L_CTL, D(10), KP0 ), 500 ));										//		[long] ICP Priority Function Button - 0 (M-SEL)
									   
								
	// China Hat
	// CHF
	MapKeyIO(&Throttle, CHF, 	brightness_up, 																	// I - brightness up for goggle/HMCS
							 	REXEC( 20, 200, "ActKey( PULSE+KEYON+DX13 );" ));								// O - ANT ELEV Knob - CW/up    (REXEC to slow down repeat press)
	// CHB
	MapKeyIO(&Throttle, CHB, 	brightness_down,																// I - brightness down for goggle/HMCS
							 	REXEC( 21, 200, "ActKey( PULSE+KEYON+DX14 );" ));								// O - ANT ELEV Knob - CCW/down    (REXEC to slow down repeat press)



	// Flaps Switch (RF Switch)
	// FLAPU
	MapKey(&Throttle, FLAPU,  		PULSE+'r');																		// RF Switch - NORM
	// FLAPM
	MapKey(&Throttle, FLAPM, 		CHAIN( DOWN+L_CTL, D(10), PULSE+'r', D(), UP+L_CTL ));							// RF Switch - QUIET
	// FLAPD
	MapKey(&Throttle, FLAPD,  		CHAIN( DOWN+R_CTL, D(10), PULSE+'r', D(), UP+R_CTL ));							// RF Switch - SILENT


	// Canopy
	// RDRNRM
	MapKeyIOUMD(&Throttle, RDRNRM, 	0, 																				// IU - 
									0,																				// OU - 
									CHAIN( DOWN+L_SHIFT, D(10), PULSE+'c', D(), UP+L_SHIFT ),						// IM - Canopy Handle - DOWN/UP
									DX11,																			// OM - Canopy - OPEN/CLOSE
									0,																				// ID - 
									0);																				// OD - 

	// Engine Operation
	// EOLIGN (overwrites common)
	MapKeyIO(&Throttle, EOLIGN,		startup, 																		// I - Start-up Sequence
									CHAIN( DOWN+R_SHIFT, D(10), PULSE+HOME, D(), UP+R_SHIFT ));						// O - Right Throttle - IDLE
	// EOLNORM in common
	// EOLMOTOR
	MapKeyIO(&Throttle, EOLMOTOR, 	CHAIN( DOWN+L_SHIFT, D(10), DOWN+'s', D(100), UP+'s', D(), UP+L_SHIFT ),		// I - JFS Switch - START 2
									0); 																			// O - 
	// EORIGN (overwrites common)
	MapKey(&Throttle, EORIGN, 		0); 																			// 
	// EORNORM in common
	// EORMOTOR in common


	// Engine Override Left
	// EFLNORM
	MapKeyIO(&Throttle, EFLNORM, 	CHAIN( 0, D(), 																	// I - MANUAL PITCH Override Switch - NORM
										   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED5) ),							// 	   LED5 OFF for MANUAL PITCH Override
									0 );																			// O - 
	// EFLOVER
	MapKeyIO(&Throttle, EFLOVER, 	CHAIN( DX22, D(),																// I - MANUAL PITCH Override Switch - OVRD
										   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED5) ),							// 	   LED5 ON for MANUAL PITCH Override
									0 );																			// O - 

	// Engine idle/off (overwrite of common)
	// IDLELON
	MapKey(&Throttle, IDLELON, 		CHAIN( DOWN+R_SHIFT, D(10), PULSE+END, D(), UP+R_SHIFT ));						// Throttle - OFF
	MapKeyR(&Throttle, IDLELON, 	CHAIN( DOWN+R_SHIFT, D(10), PULSE+HOME, D(), UP+R_SHIFT ));						// Throttle - IDLE
	// IDLERON
	MapKey(&Throttle, IDLERON, 		0);																				//

	// SC
	MapKeyIO(&Throttle, SC, 	TEMPO( CHAIN( DOWN+R_CTL, D(10), PULSE+INS, D(), UP+R_CTL ),					// I - [short] re-centre IR tracker / VR helmet
									   CHAIN( DOWN+L_SHIFT, D(10), PULSE+'`', D(), UP+L_SHIFT),					//     [long] discord game overlay
									   500),
								PULSE+'d');																		// O - Expand/FOV Button


	// U LAYER - Air-to-Air Mode
	// BSF - LED1 ON/OFF
	MapKeyIO(&Throttle, BSF, 	CHAIN( override, D(100),														// I - DOGFIGHT/Missile Override Switch - DOGFIGHT
									   DOWN+R_SHIFT, D(10), PULSE+'a', D(), UP+R_SHIFT ),						// 	   MASTER ARM Switch - MASTER ARM
								CHAIN( PULSE+'1', D(100),														// O - A-A mode on
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED1) )); 							// 	   LED1 ON for U layer
	MapKeyRIO(&Throttle, BSF, 	CHAIN( DOWN+L_CTL, D(10), PULSE+'0', D(), UP+L_CTL,								// I - DOGFIGHT/Missile Override Switch - CENTER
									   EXEC( "override_dog = 0;" 												//	   reset override status
									         "printf(\"override_dogfight = %i\\xa\", override_dog);"), D(100)),	//     print state to output window
								CHAIN( DOWN+L_CTL, D(10), PULSE+'0', D(), UP+L_CTL,								// O - DOGFIGHT/Missile Override Switch - CENTER
									   EXEC( "override_dog = 0;"												//	   Reset, in case override was started when in A-A mode
									         "printf(\"override_dogfight = %i\\xa\", override_dog);"), D(),		//     print state to output window
									   PULSE+'1', D(100),														// 	   A-A mode off
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED1) )); 							//     LED1 OFF for U layer
	
	// M LAYER - Navigation Mode
	// BSM - LED2 ON/OFF
	MapKey(&Throttle, BSM, 		CHAIN( D(200), LED(&Throttle, LED_ONOFF, LED_CURRENT+LED2)));					// LED2 ON for M layer
	MapKeyR(&Throttle, BSM, 	LED(&Throttle, LED_ONOFF, LED_CURRENT-LED2)); 									// LED2 OFF for M layer

	// D LAYER - Air-to-Ground Mode
	// BSB - LED3 ON/OFF
	MapKeyIO(&Throttle, BSB, 	CHAIN( PULSE+'2', D(100),														// I - A-G mode on
									   DOWN+R_SHIFT+R_CTL, D(10), PULSE+'a', D(), UP+R_SHIFT+R_CTL,	D(),		//     MASTER ARM Switch - SIMULATE
									   DOWN+R_SHIFT, D(10), PULSE+'e', D(), UP+R_SHIFT ),						//	   GND JETT ENABLE Switch - ENABLE
								CHAIN( PULSE+'2', D(100),														// O - A-G mode on
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED3) )); 							//     LED3 ON for D layer
	MapKeyRIO(&Throttle, BSB, 	CHAIN( PULSE+'2', D(100),														// I - A-G mode off
									   DOWN+R_CTL, D(10), PULSE+'a', D(), UP+R_CTL, D(),						//     MASTER ARM Switch - OFF
									   DOWN+R_CTL, D(10), PULSE+'e', D(), UP+R_CTL ),							//	   GND JETT ENABLE Switch - OFF
								CHAIN( PULSE+'2', D(100),														// O - A-G mode off
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED3) )); 							//     LED3 OFF for D layer


// ----------------------------- MFD --------------------------------

	// Backup on MFD for MAN RNG Knob
	MapKeyIO(&LMFD, SYMU, 		CHAIN( R_WIN+R_SHIFT, D(10), 'o' ), 											// I - Right MFD SYM Up
								CHAIN( R_WIN+R_CTL, D(10), 'o' ));												// O - MAN RNG Knob - CW

	MapKeyIO(&LMFD, SYMD, 		CHAIN( R_WIN+R_SHIFT, D(10), 'k' ), 											// I - Right MFD SYM Down
								CHAIN( R_WIN+R_CTL, D(10), 'k' ));												// O - MAN RNG Knob - CCW





}	// =====  end main()  =========================================================================


//TEST

// ------------------------ TMS for VR ------------------------------

int TMSforVR()
{
	// Target Management Switch
	// H2U
	MapKeyIO(&AVA_F16, H2U, 	CHAIN( R_CTL, D(10), UARROW ),													// I - TMS up
							 	DX15 );			 																// O - VR zoom
	// H2D
	MapKeyIO(&AVA_F16, H2D, 	CHAIN( R_CTL, D(10), DARROW ),													// I - TMS down
							 	0 );																			// O - TBD
	// H2R							
	MapKeyIO(&AVA_F16, H2R, 	CHAIN( R_CTL, D(10), RARROW ),													// I - TMS right
							 	DX18 );								 											// O - Right mouse click
	// H2L
	MapKeyIO(&AVA_F16, H2L, 	CHAIN( R_CTL, D(10), LARROW ),													// I - TMS left
							 	DX17 ); 																		// O - Left mouse click
}


// --------------------- TMS for TrackIR ----------------------------

int TMSforTrackIR()
{
	// Target Management Switch
	// H2U
	MapKeyIO(&AVA_F16, H2U, 	CHAIN( R_CTL, D(10), UARROW ),													// I - TMS up
							 	DX7 );			 																// O - zoom in (KP*) 
	// H2D
	MapKeyIO(&AVA_F16, H2D, 	CHAIN( R_CTL, D(10), DARROW ),													// I - TMS down
							 	DX9 );																			// O - zoom out (KP\)
	// H2R							
	MapKeyIO(&AVA_F16, H2R, 	CHAIN( R_CTL, D(10), RARROW ),													// I - TMS right
							 	DX8 );								 											// O - snap view right MFD (LWIN+KP6)
	// H2L
	MapKeyIO(&AVA_F16, H2L, 	CHAIN( R_CTL, D(10), LARROW ),													// I - TMS left
							 	DX10 ); 																		// O - snap view left MFD (LWIN+KP4)
}




// -------------------- AFTERBURNER DETENT --------------------------

int afterburnerdetent(int x)
{
	if (x == 1) {		// Activate detent
		SetCustomCurve(&Throttle, THR_LEFT, LIST(0,0, 25,25, 50,50, AB,AB, 100,AB));
	}
	else {				// Throttle back to full reach
		SetSCurve(&Throttle, THR_LEFT, 0, 0, 0, 0, 0);
	}
}



// ------------------------ EVENTHANDLE -----------------------------

int EventHandle(int type, alias o, int x)
{
	if ((&o == &Throttle) & (x == THR_LEFT)) {									// Detect left throttle position for afterburner detent
		if (afterburnerdetent_present & afterburnerdetent_lifted) {				// If afterburnerdetent present and detent is lifted
			if (Throttle[THR_LEFT] < (100-AB)*(2*AMAX)/100) {					//   If left throttle below afterburner detent value of AB%
				afterburnerdetent(1);											//     Re-engage detent
				afterburnerdetent_lifted = 0;									//     Detent is no longer lifted
			}
		} 
	}
	DefaultMapping(&o, x);
}