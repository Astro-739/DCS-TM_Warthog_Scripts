// DCS A-10C2 Warthog Thrustmaster Warthog Script by Astro

include "target.tmh"
include "DCS_Fighter_Common.tmc"


// -------------------- INITIALISE --------------------

int autopilot_ALThold;	
int autopilot_ATThold;

int override;	
char override_dog;		

int mastermode;
int mastermodecontrolbutton;
int mastermode_aa;
int mastermode_nav;
int deferdelay;

int brightness_up;	
int brightness_down;	

define AB 75											// detent position at AB% of full throttle reach


// -------------------- MAIN --------------------

int main()
{
	Configure(&HCougar, MODE_EXCLUDED);
	Configure(&JoystickF18, MODE_EXCLUDED);
	Configure(&A320Pilot, MODE_EXCLUDED);
	Configure(&A320Copilot, MODE_EXCLUDED);
	Configure(&TCAQuadrant12, MODE_EXCLUDED);
	Configure(&TCAQuadrant34, MODE_EXCLUDED);
	Configure(&T16000, MODE_EXCLUDED);
	Configure(&T16000L, MODE_EXCLUDED);
	Configure(&LMFD, MODE_EXCLUDED);
	Configure(&RMFD, MODE_EXCLUDED);
	Configure(&TFRPRudder, MODE_EXCLUDED);
	Configure(&TWCSThrottle, MODE_EXCLUDED);
	Configure(&TFRPHARudder, MODE_EXCLUDED);
	
	if(Init(&EventHandle)) return 1;
	
	SetKBRate(40, 50);																							// (a < b) a = pulse duration, b = delay duration
	SetKBLayout(KB_ENG);
	
	// -------------------- IOUMD Buttons --------------------
	
	SetShiftButton(&Joystick, S4, &Throttle, BSF, BSB, 0);
	
	// -------------------- COMMON --------------------
					
	dcs_fighter_common();

	// -------------------- FUNCTIONS --------------------
	
	// Autopilot function
	
	autopilot_ALThold = SEQ( CHAIN( DOWN+L_SHIFT, D(10), DOWN+'p', D(100), UP+'p', D(), UP+L_SHIFT ),			// Autopilot PITCH Switch - ALT HOLD  (extra 100ms needed by DCS)
							 CHAIN( DOWN+L_CTL, D(10), DOWN+'p', D(100), UP+'p', D(), UP+L_CTL ));				// Autopilot PITCH Switch - A/P OFF   (extra 100ms needed by DCS)
	
	autopilot_ATThold = SEQ( CHAIN( DOWN+L_ALT, D(10), DOWN+'p', D(100), UP+'p', D(), UP+L_ALT ),				// Autopilot PITCH Switch - ATT HOLD  (extra 100ms needed by DCS)
							 CHAIN( DOWN+L_CTL, D(10), DOWN+'p', D(100), UP+'p', D(), UP+L_CTL ));				// Autopilot PITCH Switch - A/P OFF   (extra 100ms needed by DCS)
	
	// Master Modes function
	mastermode = 0;																			// mastermode 0,1,2,3		NAV, GUN, CCIP, CCRP
	deferdelay = 0;
	
	mastermodecontrolbutton = CHAIN( PULSE+'m',
									 EXEC( "mastermode = (mastermode+1)%4;" ));
							   
	mastermode_aa = CHAIN( DOWN+'m', D(1000), UP+'m' );
	
	mastermode_nav = EXEC( "while (mastermode != 0)  {"
							 "DeferCall( deferdelay, &ActKey, PULSE+KEYON+'m' );"
							 "mastermode = (mastermode+1)%4;"
							 "deferdelay = deferdelay + 100;"
							 //"printf(\" mastermode %i \\xa\", mastermode);"
						   "}" );
						   


	// Brightness
							  
	brightness_up =   REXEC( 0, 50, "if(nightvision_on) ActKey( PULSE+KEYON+R_CTL+R_SHIFT+'h' );"		 		// NV Goggle brightness up
									"else ActKey( PULSE+KEYON+L_SHIFT+'h' );" );								// HMCS brightness up
	brightness_down = REXEC( 1, 50, "if(nightvision_on) ActKey( PULSE+KEYON+L_ALT+'h' );"						// NV Goggle brightness down
									"else ActKey( PULSE+KEYON+L_CTL+'h' );" );									// HMCS brightness down
																												// FLIR brightness


	// -------------------- FLIGHT STICK --------------------

	// --DCS_Fighter_Common---------
	// TG - Camera/Gun Trigger
	// S1 - Nosewheel Steering Button
	// H1 - Trim Switch
	// JOYX
	// JOYY
	// -----------------------------	


	// Nosewheel Steering Button
	// S1
	MapKeyIOUMD(&Joystick, S1, 	SEQ( PULSE+F10, PULSE+F1 ),														// IU - F10 map
							 	PULSE+INS, 																		// OU - Nosewheel Steering Button
							    SEQ( PULSE+F10, PULSE+F1 ), 													// IM - F10 map
							    PULSE+INS,															 			// OM - Nosewheel Steering Button
							    SEQ( PULSE+F10, PULSE+F1 ),								 						// ID - F10 map
							    TEMPO( PULSE+INS,																// OD - [short] Nosewheel Steering Button
									   CHAIN( DOWN+L_ALT+R_SHIFT, D(10), PULSE+'6', D(), UP+L_ALT+R_SHIFT ),	//		[long]  Switch AG weapon type (SMS page Right MFD OSB 6)
									   500 ));

	// Weapon Release Button
	// S2
	MapKeyIO(&Joystick, S2,  	CHAIN( L_SHIFT, D(10), 'e' ), 													// I - EMER STORES JETTISON Button
								DX2);																			// O - Weapon Release

	// Expand FOV Button - S3
	MapKeyUMD(&Joystick, S3,  	0,																				// U - 
							    0,																	 			// M - 
							    mastermodecontrolbutton);														// D - Master Mode Control Button


	// Target Management Switch
	// H2U
	MapKeyIO(&Joystick, H2U, 	CHAIN( L_CTL, D(10), UARROW ),													// I - TMS up
							 	USB[0x55]);		 																// O - zoom in  (KP*)
	// H2D
	MapKeyIO(&Joystick, H2D, 	CHAIN( L_CTL, D(10), DARROW ),													// I - TMS down
							 	USB[0x54]);																		// O - zoom out (KP\)
	// H2R							
	MapKeyIO(&Joystick, H2R, 	CHAIN( L_CTL, D(10), RARROW ),													// I - TMS right
							 	CHAIN( L_WIN, D(10), KP6 )); 													// O - snap view right MFD
	// H2L
	MapKeyIO(&Joystick, H2L, 	CHAIN( L_CTL, D(10), LARROW ),													// I - TMS left
							 	CHAIN( L_WIN, D(10), KP4 )); 													// O - snap view left MFD

	// Display Management Switch
	// H3U
	MapKeyIOUMD(&Joystick, H3U, CHAIN( DOWN+L_SHIFT, D(10), PULSE+'m', D(), UP+L_SHIFT ),						// IU - RWR Indicator Control SEARCH Button
								HOME,																			// OU - DMS up
								CHAIN( DOWN+R_SHIFT, D(10), PULSE+'k', D(), UP+R_SHIFT ),						// IM - kneeboard on/off
								HOME,																			// OM - DMS up
								CHAIN( DOWN+L_SHIFT, D(10), PULSE+'m', D(), UP+L_SHIFT ),						// ID - RWR Indicator Control SEARCH Button
								HOME );																			// OD - DMS up

								//TEMPO( CHAIN( DOWN+R_ALT, D(10), PULSE+';', D(), UP+R_ALT ),					// OD - DMS up
								//	   nightvision,	500 ));														// 		[long] Night Vision Goggles
	// H3D
	MapKeyIOUMD(&Joystick, H3D, CHAIN( DOWN+R_CTL, D(10), PULSE+'m', D(), UP+R_CTL ),							// IU - RWR Indicator Control MODE Button
								END,																			// OU - DMS down
								CHAIN( DOWN+R_CTL, D(10), PULSE+'k', D(), UP+R_CTL ),							// IM - kneeboard current position mark point
								END,																			// OM - DMS down
								CHAIN( DOWN+R_CTL, D(10), PULSE+'m', D(), UP+R_CTL ),							// ID - RWR Indicator Control MODE Button
								END);																			// OD - DMS down
	// H3R
	MapKeyIOUMD(&Joystick, H3R, CHAIN( L_CTL, D(10), 'm' ),														// IU - RWR Indicator Control T Button
								PGDN,																			// OU - DMS right
								PULSE+']', 																		// IM - kneeboard next page
								PGDN,																			// OM - DMS right
								CHAIN( L_CTL, D(10), 'm' ),														// ID - RWR Indicator Control T Button
								PGDN);																			// OD - DMS right
	// H3L
	MapKeyIOUMD(&Joystick, H3L, CHAIN( DOWN+R_SHIFT, D(10), PULSE+'m', D(), UP+R_SHIFT ),						// IU - RWR Indicator Control ALTITUDE Button
								DEL,																			// OU - DMS left
								PULSE+'[', 																		// IM - kneeboard previous page
								DEL,																			// OM - DMS left
								CHAIN( DOWN+R_SHIFT, D(10), PULSE+'m', D(), UP+R_SHIFT ),						// ID - RWR Indicator Control ALTITUDE Button
								DEL);																			// OD - DMS left

 	// Countermeasure Management Switch
	// H4U
	MapKeyIO(&Joystick, H4U, 	CHAIN( DOWN+L_SHIFT+L_CTL, D(10), PULSE+'d', D(), UP+L_SHIFT+L_CTL ),			// I - ECM XMIT Switch - Up
							 	'7');																			// O - Countermeasures Management Switch - Fwd
	// H4D
	MapKeyIO(&Joystick, H4D, 	CHAIN( DOWN+L_SHIFT+L_CTL, D(10), PULSE+'c', D(), UP+L_SHIFT+L_CTL ),			// I - ECM XMIT Switch - Down
							 	'8'); 																			// O - Countermeasures Management Switch - Aft
	// H4R
	MapKeyIO(&Joystick, H4R, 	CHAIN( DOWN+L_CTL, D(10), PULSE+'f', D(), UP+L_CTL ), 							// I - PROGRAM Knob - CW
			  				 	'0');																			// O - Countermeasures Management Switch - Right
	// H4L
	MapKeyIO(&Joystick, H4L, 	CHAIN( DOWN+L_SHIFT, D(10), PULSE+'f', D(), UP+L_SHIFT ), 						// I - PROGRAM Knob - CCW
							 	'9'); 																			// O - Countermeasures Management Switch - Left
	// H4P
	MapKeyIO(&Joystick, H4P, 	0, 																				// I - 
							 	'-');																			// O - Countermeasures Management Switch - Push



 
	// -------------------- THROTTLE --------------------

	// --DCS_Fighter_Common---------
	// Left Throttle Button
	// MSU/D/P
	// EACON/OFF Landing Gear
	// EOLNORM
	// EORNORM/MOTOR
	// SC
	// APUON/OFF
	// RDRNRM/DIS Canopy
	// IDLELON/IDLERON
	// THR_LEFT
	// THR_RIGHT
	// THR_FC
	// SCX
	// SCY
	// -----------------------------



	// Autopilot
	// APENG	
	MapKeyIO(&Throttle, APENG, 	autopilot_ATThold,																// I - Autopilot ATT on/off (Autopilot PITCH Switch - ATT HOLD)
								autopilot_ALThold );															// O - Autopilot ALT on/off (Autopilot PITCH Switch - ALT HOLD)
	// APDIS
	MapKey(&Throttle, APDIS, 	0); 																			// 
	// APPAT
	MapKey(&Throttle, APPAT, 	CHAIN( DOWN+L_SHIFT, D(10), PULSE+'a', D(), UP+L_SHIFT )); 						// Autopilot ROLL Switch - HDG SEL
	// APAH
	MapKey(&Throttle, APAH, 	CHAIN( DOWN+L_CTL, D(10), PULSE+'a', D(), UP+L_CTL ));							// Autopilot ROLL Switch - ATT HOLD
	// APALT
	MapKey(&Throttle, APALT, 	CHAIN( DOWN+L_ALT, D(10), PULSE+'a', D(), UP+L_ALT ));							// Autopilot ROLL Switch - STRG SEL

	// MIC Switch (Transmit Switch)
	// MSR
	MapKeyIOUMD(&Throttle, MSR, flight_attackmytarget, 															// IU - Flight - Attack My Target
								CHAIN( L_CTL, D(10), 'i' ),														// OU - Transmit Switch - IFF OUT
								EXEC( "if(Throttle[EFROVER]) ActKey( PULSE+KEYON+L_WIN+'a' );"					// IM - A/A refueling - "Ready for precontact" radio call
									  "else ActKey( PULSE+KEYON+L_ALT+USB[0x34] );" ),							//      Rearming and Refueling Window   (L_ALT+')
								CHAIN( L_CTL, D(10), 'i' ),														// OM - Transmit Switch - IFF OUT
								flight_attackmytarget, 															// ID - Flight - Attack My Target
								CHAIN( L_CTL, D(10), 'i' ));													// OD - Transmit Switch - IFF OUT
	// MSL
	MapKeyIOUMD(&Throttle, MSL, awacs_requestbogeydope,								 							// IU - Request AWACS Bogey Dope
								CHAIN( L_SHIFT, D(10), 'i' ), 													// OU - Transmit Switch - IFF IN 
								awacs_requestbogeydope,															// IM - Request AWACS Bogey Dope
								CHAIN( L_SHIFT, D(10), 'i' ), 													// OM - Transmit Switch - IFF IN
								awacs_requestbogeydope,															// ID - Request AWACS Bogey Dope
								CHAIN( L_SHIFT, D(10), 'i' ));													// OD - Transmit Switch - IFF IN

	// Coolie Switch
	// CSU
	MapKeyIOUMD(&Throttle, CSU, CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x56], D(), UP+L_CTL ),						// IU - ICP DED Increment/Decrement Switch - Decrement   (L_CTL+NUM-)
								'u',																			// OU - Coolie Switch Up
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x56], D(), UP+L_CTL ),						// IM - ICP DED Increment/Decrement Switch - Decrement   (L_CTL+NUM-)
								'u',																			// OM - Coolie Switch Up
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x56], D(), UP+L_CTL ),						// ID - ICP DED Increment/Decrement Switch - Decrement   (L_CTL+NUM-)
								'u' );																			// OD - Coolie Switch Up
	// CSD
	MapKeyIOUMD(&Throttle, CSD, CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x57], D(), UP+L_CTL ),						// IU - ICP DED Increment/Decrement Switch - Increment   (L_CTL+NUM+)
								'j',																			// OU - Coolie Switch Down
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x57], D(), UP+L_CTL ),						// IM - ICP DED Increment/Decrement Switch - Increment   (L_CTL+NUM+)
								'j',																			// OM - Coolie Switch Down
								CHAIN( DOWN+L_CTL, D(10), PULSE+USB[0x57], D(), UP+L_CTL ),						// ID - ICP DED Increment/Decrement Switch - Increment   (L_CTL+NUM+)
								'j' );																			// OD - Coolie Switch Down
	// CSR
	MapKeyIOUMD(&Throttle, CSR, 0, 																				// IU - 
								'k',																			// OU - Coolie Switch Right
								0, 																				// IM - 
								'k',																			// OM - Coolie Switch Right
								0, 																				// ID - 
								'k' );																			// OD - Coolie Switch Right
	// CSL
	MapKeyIOUMD(&Throttle, CSL, CHAIN( L_CTL, D(10), LARROW ), 													// IU - ICP Data Control Switch - RET (dobber return)
								'h',																			// OU - Coolie Switch Left
								CHAIN( L_CTL, D(10), LARROW ), 													// IM - ICP Data Control Switch - RET (dobber return)
								'h',																			// OM - Coolie Switch Left
								CHAIN( L_CTL, D(10), LARROW ), 													// ID - ICP Data Control Switch - RET (dobber return)
								'h' );																			// OD - Coolie Switch Left

	// Speedbrake Switch
	// SPDF
	MapKeyIO(&Throttle, SPDF, 	DX31,																			// I - Speed Brake Switch Forward
							   	CHAIN( DOWN+R_SHIFT, D(10), PULSE+USB[0x55], D(), UP+R_SHIFT ));				// O - snap zoom max   (R_SHIFT+KP*)
	MapKeyRIO(&Throttle, SPDF, 	0,																				// I - 
								PULSE+KPENT );																	// O - snap zoom normal
	// SPDB
	MapKeyIOUMD(&Throttle, SPDB, CHAIN( L_CTL, D(10), 'b' ),													// IU - Speed Brake Switch Aft
								CHAIN( DOWN+R_SHIFT, D(10), PULSE+USB[0x54], D(), UP+R_SHIFT ),					// OU - snap zoom min    (R_SHIFT+KP/)
								CHAIN( L_CTL, D(10), 'b' ),														// IM - Speed Brake Switch Aft
								'w',																			// OM - wheel brake
								CHAIN( L_CTL, D(10), 'b' ),														// ID - Speed Brake Switch Aft
								CHAIN( DOWN+R_SHIFT, D(10), PULSE+USB[0x54], D(), UP+R_SHIFT ));				// OD - snap zoom min    (R_SHIFT+KP/)
	MapKeyRIOUMD(&Throttle, SPDB, 0,																			// IU - 
								PULSE+KPENT,																	// OU - snap zoom normal
								0,																				// IM - 
								0,																				// OM - 
								0,																				// ID - 
								PULSE+KPENT);																	// OD - snap zoom normal
								
	// China Hat
	// CHF
	MapKeyIO(&Throttle, CHF, 	brightness_up, 																	// I - brightness up for goggle/HMCS
							 	'v' );																			// O - China Hat Fwd
	// CHB
	MapKeyIO(&Throttle, CHB, 	brightness_down,																// I - brightness down for goggle/HMCS
							 	'c' );																			// O - China Hat Aft

	// Pinky Switch (exterior lighting)
	// PSF
	MapKeyIO(&Throttle, PSF, 	CHAIN( DOWN+R_ALT, D(10), PULSE+'l', D(), UP+R_ALT ),								// I - FLASH STEADY Switch - FLASH/STEADY
								CHAIN( DOWN+L_CTL, D(10), PULSE+'p', D(), UP+L_CTL, D(), 							// O - Exterior Light per Lighting Panel (pinky switch aft)
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) )); 								//     LED4 ON								
	MapKeyRIO(&Throttle, PSF, 	0, 																					// I - 
							  	CHAIN( DOWN+L_ALT, D(10), PULSE+'p', D(), UP+L_ALT ));								// O - position lights to dimsteady / strobes off (pinky switch fwd)
	// PSB
	MapKeyIO(&Throttle, PSB, 	SEQ( CHAIN( DOWN+R_SHIFT+L_ALT, D(10), PULSE+'l', D(), UP+R_SHIFT+L_ALT, D(),		// I - WING/TAIL Switch - DIM
											DOWN+R_CTL+L_ALT, D(10), PULSE+'l', D(), UP+R_CTL+L_ALT ),				// 	   FUSELAGE Switch - DIM
									 CHAIN( DOWN+R_SHIFT+L_SHIFT, D(10), PULSE+'l', D(), UP+R_SHIFT+L_SHIFT, D(),	//	   WING/TAIL Switch - BRT
											DOWN+R_CTL+L_SHIFT, D(10), PULSE+'l', D(), UP+R_CTL+L_SHIFT )),			//	   FUSELAGE Switch - BRT
							 	CHAIN( DOWN+L_SHIFT, D(10), PULSE+'p', D(), UP+L_SHIFT, D(), 						// O - all covert (pinky switch centre)
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED4) )); 								//     LED4 OFF
	MapKeyRIO(&Throttle, PSB, 	0, 																					// I - 
							  	CHAIN( DOWN+L_ALT, D(10), PULSE+'p', D(), UP+L_ALT, D(),							// O - position lights to dimsteady / strobes off (pinky switch fwd)
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED4) )); 								//     LED4 ON
								
	// LDGH
	MapKeyIOUMD(&Throttle, LDGH, 	0, 																				// IU - 
									CHAIN( R_CTL, D(10), 'c' ),														// OU - Master Caution Button
									CHAIN( DOWN+R_CTL, D(10), PULSE+BSP, D(), UP+R_CTL ), 							// IM - Upload F-16 DTC (Data Cartridge)
									CHAIN( R_CTL, D(10), 'c' ),														// OM - Master Caution Button
									0, 																				// ID - 
									CHAIN( R_CTL, D(10), 'c' ));													// OD - Master Caution Button

	// Flaps Switch 
	// FLAPU
	MapKey(&Throttle, FLAPU,  		CHAIN( DOWN+L_SHIFT, D(10), PULSE+'f', D(), UP+L_SHIFT ));						// Flaps Up
	MapKeyR(&Throttle, FLAPU,  		PULSE+'f');																		// Flaps Down (to middle)	
	// FLAPM
	MapKey(&Throttle, FLAPM, 		0);																				// 
	// FLAPD
	MapKey(&Throttle, FLAPD,  		PULSE+'f');																		// Flaps Down (to full)
	MapKeyR(&Throttle, FLAPD,  		CHAIN( DOWN+L_SHIFT, D(10), PULSE+'f', D(), UP+L_SHIFT ));						// Flaps Up (to middle)

	// Canopy
	// RDRNRM
	MapKeyIOUMD(&Throttle, RDRNRM, 	0, 																				// IU - 
									0,																				// OU - 
									0,																				// IM -
									CHAIN( DOWN+L_CTL, D(10), PULSE+'c', D(), UP+L_CTL ),							// OM - Canopy - OPEN/CLOSE
									0,																				// ID - 
									0);																				// OD - 

	// Engine Operation
	// EOLIGN
	MapKeyIO(&Throttle, EOLIGN,		SEQ( CHAIN( L_ALT, D(10), 's' ), 												// I - MAIN PWR Switch - BATT
										 CHAIN( R_SHIFT, D(10), 's' ), 												// 	   MAIN PWR Switch - MAIN PWR
										 CHAIN( R_CTL, D(10), 's' )), 												//     JFS Switch - START 1
									CHAIN( DOWN+R_SHIFT, D(10), PULSE+HOME, D(), UP+R_SHIFT ));						// O - Throttle - IDLE
	// EOLMOTOR
	MapKeyIO(&Throttle, EOLMOTOR, 	CHAIN( DOWN+L_SHIFT, D(10), DOWN+'s', D(100), UP+'s', D(), UP+L_SHIFT ),		// I - JFS Switch - START 2
									0); 																			// O - 
	// EORIGN
	MapKey(&Throttle, EORIGN, 		0); 																			// 

	// Engine Override
	// EFLNORM
	MapKeyIO(&Throttle, EFLNORM, 	0,																				// I - 
									CHAIN( 0, D(), 																	// O - MANUAL PITCH Override Switch - NORM
										   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED5) ));							// 	   LED5 OFF for MANUAL PITCH Override
	// EFLOVER
	MapKeyIO(&Throttle, EFLOVER, 	0,																				// I - 
									CHAIN( DX32, D(),																// O - MANUAL PITCH Override Switch - OVRD
										   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED5) ));							// 	   LED5 ON for MANUAL PITCH Override
	// EFRNORM
	MapKeyIO(&Throttle, EFRNORM, 	CHAIN( DOWN+L_CTL+L_ALT, D(10), PULSE+'h', D(), UP+L_CTL+L_ALT ),				// I - HOOK Switch - UP
									0);																				// O - 
	// EFROVER
	MapKeyIO(&Throttle, EFROVER, 	CHAIN( DOWN+L_CTL+L_SHIFT, D(10), PULSE+'h', D(), UP+L_CTL+L_SHIFT ),			// I - HOOK Switch - DOWN
									CHAIN( DOWN+R_SHIFT, D(10), PULSE+'f', D(), UP+R_SHIFT,	D(), 					// O - AIR REFUEL Switch - OPEN
										   //EXEC( "SetSCurve(&Joystick, JOYX, 0, 0, 0, 0, -1);"						//	    set joyX curves for aerial refuelling
										   // 	 "SetSCurve(&Joystick, JOYY, 0, 0, 0, 0, -1);" ), D(), 				// 	  	set joyY curves for aerial refuelling
										   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED5) ));							// 		LED5 ON for Aerial Refuelling
	MapKeyRIO(&Throttle, EFROVER, 	0,																				// I - 
									CHAIN( DOWN+R_CTL, D(10), PULSE+'f', D(), UP+R_CTL, D(),						// O - AIR REFUEL Switch - CLOSE
										   //EXEC( "SetSCurve(&Joystick, JOYX, 0, 0, 0, 0, 0);"						//		set joyX curves back to normal
										   //	 "SetSCurve(&Joystick, JOYY, 0, 0, 0, 0, 0);" ), D(), 				//		set joyY curves back to normal
										   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED5) ));							// 		LED5 OFF for Aerial Refuelling

	// Engine idle/off
	// IDLELON
	MapKey(&Throttle, IDLELON, 		CHAIN( DOWN+R_SHIFT, D(10), PULSE+END, D(), UP+R_SHIFT ));						// Throttle - OFF
	MapKeyR(&Throttle, IDLELON, 	CHAIN( DOWN+R_SHIFT, D(10), PULSE+HOME, D(), UP+R_SHIFT ));						// Throttle - IDLE
	// IDLERON
	MapKey(&Throttle, IDLERON, 		0);																				//

	// SC
	MapKeyIO(&Throttle, SC, 	TEMPO( CHAIN( DOWN+R_CTL, D(10), PULSE+HOME, D(), UP+R_CTL ),					// I - [short] re-centre IR tracker
									   CHAIN( DOWN+L_SHIFT, D(10), PULSE+'`', D(), UP+L_SHIFT),					//     [long] discord game overlay
									   500),
								PULSE+'d');																		// O - Expand/FOV Button


	// U LAYER - Air-to-Air Mode
	// BSF - LED1 ON/OFF
	MapKeyIO(&Throttle, BSF, 	0,																				// I - 
								CHAIN( mastermode_aa, D(100),														// O - A-A mode on
									   DOWN+R_SHIFT, D(10), PULSE+'a', D(), UP+R_SHIFT,							// 	   Master switch ARM
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED1) )); 							// 	   LED1 ON for U layer
	MapKeyRIO(&Throttle, BSF, 	0,																				// I - 
								CHAIN( PULSE+'m', D(),															// O - A-A mode off
									   DOWN+R_CTL, D(10), PULSE+'a', D(), UP+R_CTL,								//     Master switch SAFE
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED1) )); 							//     LED1 OFF for U layer
	
	// M LAYER - Navigation Mode
	// BSM - LED2 ON/OFF
	MapKey(&Throttle, BSM, 		CHAIN( D(200), LED(&Throttle, LED_ONOFF, LED_CURRENT+LED2)));					// LED2 ON for M layer
	MapKeyR(&Throttle, BSM, 	LED(&Throttle, LED_ONOFF, LED_CURRENT-LED2)); 									// LED2 OFF for M layer

	// D LAYER - Air-to-Ground Mode
	// BSB - LED3 ON/OFF
	MapKeyIO(&Throttle, BSB, 	0,																				// I -
								CHAIN( mastermodecontrolbutton, D(100),											// O - A-G mode on
									   DOWN+R_SHIFT, D(10), PULSE+'a', D(), UP+R_SHIFT,							//     Master switch ARM	
									   LED(&Throttle, LED_ONOFF, LED_CURRENT+LED3) )); 							//     LED3 ON for D layer
	MapKeyRIO(&Throttle, BSB, 	0,																				// I -
								CHAIN( mastermode_nav, D(100),													// O - A-G mode off
									   DOWN+R_CTL, D(10), PULSE+'a', D(), UP+R_CTL,								//     Master switch SAFE
									   LED(&Throttle, LED_ONOFF, LED_CURRENT-LED3) )); 							//     LED3 OFF for D layer


}

// -------------------- AFTERBURNER DETENT --------------------

int afterburnerdetent(int x)
{
	if (x == 1) {		// Activate detent
		SetCustomCurve(&Throttle, THR_LEFT, LIST(0,0, 25,25, 50,50, AB,AB, 100,AB));
	}
	else {				// Throttle back to full reach
		SetSCurve(&Throttle, THR_LEFT, 0, 0, 0, 0, 0);
	}
}



// -------------------- EVENTHANDLE --------------------

int EventHandle(int type, alias o, int x)
{
	if ((&o == &Throttle) & (x == THR_LEFT)) {									// Detect left throttle position for afterburner detent
		if (afterburnerdetent_present & afterburnerdetent_lifted) {				// If afterburnerdetent present and detent is lifted
			if (Throttle[THR_LEFT] < (100-AB)*(2*AMAX)/100) {					//   If left throttle below afterburner detent value of AB%
				afterburnerdetent(1);											//     Re-engage detent
				afterburnerdetent_lifted = 0;									//     Detent is no longer lifted
			}
		} 
	}
	DefaultMapping(&o, x);
}